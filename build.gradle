plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'com.modrinth.minotaur'
    id 'com.matthewprenger.cursegradle'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

String modVersion
if (System.getenv("RELEASE_TAG") != null) {
    String releaseTag = System.getenv("RELEASE_TAG")
    modVersion = releaseTag.substring(1)
    println("Detected Release Version: $modVersion")
} else {
    modVersion = project.mod_version
    println("Detected Local Version: $modVersion")
}

archivesBaseName = project.archives_base_name
version = modVersion
group = project.maven_group

repositories {
    maven {
        // EMI & Mod Menu
        name "TerraformersMC"
        url "https://maven.terraformersmc.com/releases/"
    }
    maven {
        // TechReborn
        name "CurseMaven"
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://mvn.devos.one/snapshots" }
    maven { url = "https://maven.shedaniel.me/"  }
    maven { url = "https://maven.wispforest.io" } // TOP
    maven { url = "https://maven.blamejared.com" } // CraftTweaker, Immersive Engineering
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }
    maven { url = "https://ladysnake.jfrog.io/artifactory/mods" }
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // EMI
    modImplementation("dev.emi:emi:${project.emi_version}") {
        transitive = false
    }

    // Tech Reborn
    modCompileOnly "curse.maven:techreborn-233564:${project.tech_reborn_id}"
    modRuntimeOnly "curse.maven:techreborn-233564:${project.tech_reborn_id}"
    modCompileOnly "curse.maven:reborncore-237903:${project.reborn_core_id}"
    modRuntimeOnly "curse.maven:reborncore-237903:${project.reborn_core_id}"
    modCompileOnly "teamreborn:energy:${project.energy_version}"
    modRuntimeOnly "teamreborn:energy:${project.energy_version}"

    // Hephaestus
    modCompileOnly "net.minecraftforge:forgeconfigapiport-fabric:${project.forge_config_version}"
    modCompileOnly "slimeknights.mantle:Mantle:${project.mantle_version}"
    modCompileOnly "io.github.fabricators_of_create:Porting-Lib:${project.port_lib_version}"
    modCompileOnly "slimeknights.tconstruct:Hephaestus:${project.hephaestus_version}"
    // TODO: get hephaestus working in the dev env

    // Mod Menu
    modRuntimeOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = project.mrProjectId
    versionNumber = modVersion
    versionType = project.mrVeresionType
    def file = rootProject.file("changelogs/changelog-v${modVersion}.md")
    if (file.exists()) {
        changelog = file.text
    }
    uploadFile = tasks.remapJar
    additionalFiles = [tasks.sourcesJar]
    gameVersions = project.mrGameVersions.split("\\s*,\\s*").toList()
    loaders = project.mrLoaders.split("\\s*,\\s*").toList()
    dependencies {
        for (final def projectId in project.mrDependencies.split("\\s*,\\s*")) {
            required.project projectId
        }
    }
    syncBodyFrom = rootProject.file("README.md").text
}

String curseApiKey = System.getenv("CURSE_API_KEY")
if (curseApiKey != null) {
    curseforge {
        apiKey = curseApiKey
        project {
            id = project.cfProjectId
            changelogType = 'markdown'
            changelog = rootProject.file("changelogs/changelog-v${modVersion}.md")
            releaseType = project.cfReleaseType
            project.cfMinecraftVersions.split("\\s*,\\s*").each {addGameVersion(it)}
            mainArtifact(tasks.remapJar)
            relations {
                project.cfDependencies.split("\\s*,\\s*").each {requiredDependency(it)}
            }
        }
        options {
            forgeGradleIntegration = false
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        if (System.getenv("PUBLISH_REPO") != null) {
            maven {
                name = "publishRepo"
                url = uri(System.getenv("PUBLISH_REPO"))
            }
        }
    }
}
